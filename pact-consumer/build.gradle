apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode rootProject.ext.pact_version_code
        versionName rootProject.ext.pact_version
    }
    buildTypes {
        debug {
            debuggable true
        }
        release {
            minifyEnabled false
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    
    api "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    api "com.squareup.okhttp3:mockwebserver:$mockserver_version"
    api "org.apache.commons:commons-collections4:$apache_collections_version"
    api "io.gatling:jsonpath_2.11:$jsonpath_version"

    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    api "com.google.code.gson:gson:$gson_version"
    api "org.apache.commons:commons-lang3:$apache_commons_version"
    api "joda-time:joda-time:$jodatime_version"
    api "com.github.mifmif:generex:$generex_version"
    api "com.github.zafarkhaja:java-semver:$semver_version"

    testImplementation "junit:junit:$test_junit_version"
    testImplementation "org.json:json:$test_json_version"
    testImplementation "org.mockito:mockito-core:$test_mockito_version"

    // The Android vm does not like the library "org.apache.httpcomponents:httpcore:4.4.4"
    // because it already contains parts from it that are final for example but a super class in the lib.
    // The required java classes are added manually into org.apache.http...
}

apply plugin: 'maven-publish'

def coreAarFile = file('build/outputs/aar/pact-consumer-release.aar')
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

publishing.publications {
    consumer(MavenPublication) {
        artifact coreAarFile
        groupId rootProject.ext.group
        artifactId rootProject.ext.lib_artifact
        version rootProject.ext.pact_version
        //The publication doesn't know about our dependencies, so we have to manually add them to the pom
        pom.withXml {
            def dependenciesNode = asNode().appendNode('dependencies')
            consumeNodes(dependenciesNode, configurations.api.allDependencies)
        }
    }
}

static def consumeNodes(Node dependenciesNode, DependencySet dependencySet) {
    //Iterate over the dependencies (we don't want the test ones), adding a <dependency> node for each
    dependencySet.each {
        if(it instanceof ExternalModuleDependency) {
            def dependencyNode = dependenciesNode.appendNode('dependency')
            dependencyNode.appendNode('groupId', it.group)
            dependencyNode.appendNode('artifactId', it.name)
            dependencyNode.appendNode('version', it.version)
        }
    }
}

publishing.repositories {
    mavenLocal()
}